//******************************************************************************
//**                       Importar paquetes                                  **
//******************************************************************************
package analizadores;

//******************************************************************************
//**                       Importar librería                                  **
//******************************************************************************
import java_cup.runtime.Symbol;
import almacenamiento.Clave;
import java.util.ArrayList;

//PARSER 
parser code
{:
    //Codigo visible

    public int er = 0;
    public int enr = 0;

    public void syntax_error(Symbol s){
        System.out.println("Error R de sintaxis: "+ s.value +" Linea "+(s.left+1)+" columna "+(s.right+1) );
        er = er+1;
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.out.println("Error NR de sintaxis: "+ s.value +" Linea "+(s.left+1)+" columna "+(s.right+1) );
        enr = enr +1;
    }

:}

action code
{://Codigo de acciones

    public String algo = "";
    public ArrayList<Clave> listaClaves = new ArrayList<Clave>();
    public int dimension = 0;
    public ArrayList<Object[]> listaRegistros = new ArrayList<Object[]>();
    public ArrayList<String> temporal = new ArrayList<String>();
    public int buscando = 0;

:}


//******************************************************************************
//**                     Seccion de terminales                                **
//******************************************************************************

terminal R_CLAVES, R_REGISTROS, CORCHETEABRE, CORCHETECIERRA, IGUAL, SPACE, ENTER, COMA, LLAVEABRE, LLAVECIERRA, COMENTARIOU, COMENTARIOM;
terminal String CADENA, NUMERICO;

//******************************************************************************
//**                     Seccion de no terminales                             **
//******************************************************************************
nonterminal S,INICIO, DEF_CLAVES, DEF_REGISTROS, LISTA_REGISTROS, SECCIONCLAVES;
nonterminal String TIPO;
//******************************************************************************
//**                       Inicio con:                                        **
//******************************************************************************
start with S;
//******************************************************************************
//**                            Reglas                                        **
//******************************************************************************
S::= INICIO
;

INICIO::=   SECCIONCLAVES R_REGISTROS IGUAL CORCHETEABRE DEF_REGISTROS CORCHETECIERRA
            {:
                String imprimir = "Fin del análisis";
                System.out.println(imprimir);
            :}
;

SECCIONCLAVES::=    R_CLAVES IGUAL CORCHETEABRE DEF_CLAVES CORCHETECIERRA
                    {:
                        if(listaClaves.size() > 0){
                            dimension = listaClaves.size();
                            System.out.println("La dimensión es" + dimension);
                            temporal.clear();
                        }else{
                            System.out.println("No se han detectado claves");
                            temporal.clear();
                        }
                    :}
;


DEF_CLAVES::=   DEF_CLAVES COMA CADENA:a
                {:
                    if(!a.equals("")){
                        Clave newClave = new Clave(a, 0);
                        listaClaves.add(newClave);
                    }
                :}
            |   CADENA:b
                {:
                    if(!b.equals("")){
                        Clave newClave = new Clave(b, 0);
                        listaClaves.add(newClave);
                    }
                :}
;

DEF_REGISTROS::=    DEF_REGISTROS LLAVEABRE LISTA_REGISTROS LLAVECIERRA
                    {:
                        if(temporal.size() == dimension){
                            System.out.println("La dimensión está bien");
                            //si es la primera vez que asigne tipos
                            if(buscando == 0){
                                for(int i = 0; i < temporal.size(); i++){
                                    if(temporal.get(i).matches("[+-]?\\d*(\\.\\d+)?") && temporal.get(i).equals("")==false){
                                        listaClaves.get(i).tipo = 0;
                                    }else{
                                        listaClaves.get(i).tipo = 1;
                                    }
                                }
                                buscando = 1;
                            }

                            //introduce el arreglo al array de registros
                        }else{
                            System.out.println("La dimension está mal");
                        }
                        temporal.clear();
                    :}
                |   
;

LISTA_REGISTROS::=  LISTA_REGISTROS COMA TIPO:a
                    {:
                        temporal.add(a);
                    :}
                |   TIPO:b
                    {:
                        temporal.add(b);
                    :}
;

TIPO::=     NUMERICO:a
            {:
                RESULT = a;
            :}
        |   CADENA:b
            {:
                RESULT = b;
            :}
;

